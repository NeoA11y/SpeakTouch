plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def versionMajor = 0 // 0..Infinity
def versionMinor = 1 // 0..9
def versionPatch = 1 // 0..9

def buildVersionCode = versionMajor * 100 + versionMinor * 10 + versionPatch
def buildVersionName = "$versionMajor.$versionMinor.$versionPatch"

android {
    namespace 'com.neo.speaktouch'
    compileSdk 33

    signingConfigs {
        release {
            def keystoreProperties = getKeyStoreProperties()

            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    defaultConfig {
        applicationId "com.neo.speaktouch"
        minSdk 22
        targetSdk 33
        versionCode buildVersionCode
        versionName buildVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false

            versionNameSuffix '-dev'
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

task versionCode {
    doLast {
        println "versionCode is $buildVersionCode from v$buildVersionName"
    }
}

def getKeyStoreProperties() {

    def keystoreProperties = new Properties()

    def file = rootProject.file("keystore.properties")

    def fileInputStream = new FileInputStream(file)

    keystoreProperties.load(fileInputStream)

    return keystoreProperties
}
